#Область СлужебныйПрограммныйИнтерфейс

// Процедура загружает объекты в информационную базу.
//
Процедура ПринятьСообщениеЗагрузки(СообщениеОбмена) Экспорт
	
	СообщениеОбмена = СтрЗаменить(СообщениеОбмена, ОбменМобильноеПриложениеВызовСервера.URIПространстваИменСервиса(),
	"http://v8.1c.ru/8.1/data/enterprise/current-config");
	
	НачатьТранзакцию();
	
	ЧтениеXML = Новый ЧтениеXML;
	ЧтениеXML.УстановитьСтроку(СообщениеОбмена);
	
	ЧтениеСообщения = ПланыОбмена.СоздатьЧтениеСообщения();
	ЧтениеСообщения.НачатьЧтение(ЧтениеXML);
	
	Пока СериализаторXDTO.ВозможностьЧтенияXML(ЧтениеXML) Цикл
		
		ТипДанныхXML = ПолучитьXMLТип(ЧтениеXML);
		
		Если ТипДанныхXML.ИмяТипа = "ObjectDeletion" Тогда
			
			ДанныеОбъект = СериализаторXDTO.ПрочитатьXML(ЧтениеXML);
			
		Иначе
			
			ТипОбъектаXDTO	= ФабрикаXDTO.Тип(ТипДанныхXML.URIПространстваИмен, ТипДанныхXML.ИмяТипа);
			ОбъектXDTOСпр	= ФабрикаXDTO.Создать(ТипОбъектаXDTO);
			Данные			= ФабрикаXDTO.ПрочитатьXML(ЧтениеXML);
			
			Для Каждого Свойство Из Данные.Свойства() Цикл
				
				Если Свойство.Имя = "Owner" Тогда
					Продолжить;
				КонецЕсли;
				
				Если ТипЗнч(Данные[Свойство.Имя]) = Тип("ОбъектXDTO") Тогда
					ОбъектXDTOСпр[Свойство.Имя] = СериализаторXDTO.ПрочитатьXDTO(Данные[Свойство.Имя]);
				Иначе
					ОбъектXDTOСпр[Свойство.Имя] = Данные[Свойство.Имя];
				КонецЕсли;
				
			КонецЦикла;
			
			ДанныеОбъект = СериализаторXDTO.ПрочитатьXDTO(ОбъектXDTOСпр);
			
			Если ТипДанныхXML.ИмяТипа = "CatalogObject.БанковскиеСчета" Тогда
				ДанныеОбъект.Владелец = Справочники.Организации.ПолучитьСсылку(Новый УникальныйИдентификатор(Данные.Owner));
			КонецЕсли;
			
			Если ДанныеОбъект <> Неопределено Тогда
				ДанныеОбъект.Записать();
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	ЗафиксироватьТранзакцию();
	
	ЧтениеСообщения.ЗакончитьЧтение();
	ЧтениеXML.Закрыть();
	
КонецПроцедуры // ПринятьСообщениеЗагрузки()

// Процедура выгружает объекты в центральную базу.
//
Процедура ПодготовитьСообщениеВыгрузки(СообщениеОбмена) Экспорт
	
	УзелОбмена = ОбменМобильноеПриложениеВызовСервера.УзелЦентральнойБазы();
	
	ЗаписьXML = Новый ЗаписьXML;
	ЗаписьXML.УстановитьСтроку("UTF-8");
	ЗаписьXML.ЗаписатьОбъявлениеXML();
	
	ЗаписьСообщения = ПланыОбмена.СоздатьЗаписьСообщения();
	ЗаписьСообщения.НачатьЗапись(ЗаписьXML, УзелОбмена);
	
	ЗаписьXML.ЗаписатьСоответствиеПространстваИмен("xsi", "http://www.w3.org/2001/XMLSchema-instance");
	ЗаписьXML.ЗаписатьСоответствиеПространстваИмен("v8", "http://v8.1c.ru/data");
	
	ЗаписьСообщения.ЗакончитьЗапись();
	
	СообщениеОбмена = ЗаписьXML.Закрыть();
	
КонецПроцедуры // ПодготовитьСообщениеВыгрузки()

#КонецОбласти