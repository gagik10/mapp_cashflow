#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Параметры.Свойство("ВызовИзФормыОбмена", ВызовИзФормыОбмена);
	
	Набор = Константы.СоздатьНабор();
	Набор.Прочитать();
	ЗначениеВРеквизитФормы(Набор, "НаборКонстант");
	
	АдресЦентральнойБазыПриОткрытии 		= НаборКонстант.АдресЦентральнойБазы;
	ПользовательЦентральнойБазыПриОткрытии	= НаборКонстант.ПользовательЦентральнойБазы;
	
	УстановитьВидимостьИДоступность();
	
КонецПроцедуры // ПриСозданииНаСервере()

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если ВызовИзФормыОбмена И Не ОбменМобильноеПриложениеВызовСервера.ЕстьНастройкиОбмена() Тогда
		
		ТекстВопроса = НСтр("ru='Не указан адрес приложения."
		"Отменить синхронизацию данных?';en='No address application."
		"Cancel data synchronization?'");
		
		ТекстЗаголовка = НСтр("en='Synchronization Settings';ru='Настройки синхронизации данных'");
		
		Ответ = Вопрос(ТекстВопроса, РежимДиалогаВопрос.ДаНет, 0, , ТекстЗаголовка);
		Если Ответ = КодВозвратаДиалога.Нет Тогда
			Отказ = Истина;
			Возврат;
		КонецЕсли;
		
	КонецЕсли;
	
	ПроверитьИзменениеНастроекОбмена();
	
КонецПроцедуры // ПередЗакрытием()

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура АдресЦентральнойБазыПриИзменении(Элемент)
	
	ЗаписатьКонстанты();
	
КонецПроцедуры // АдресЦентральнойБазыПриИзменении()

&НаКлиенте
Процедура ПользовательЦентральнойБазыПриИзменении(Элемент)
	
	УстановитьНаименованиеУстройства();
	ЗаписатьКонстанты();
	
КонецПроцедуры // ПользовательЦентральнойБазыПриИзменении()

&НаКлиенте
Процедура ПарольПриИзменении(Элемент)
	
	ЗаписатьКонстанты();
	
КонецПроцедуры // ПарольПриИзменении()

&НаКлиенте
Процедура ПарольНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Элементы.Пароль.РежимПароля = Не Элементы.Пароль.РежимПароля;
	
КонецПроцедуры // ПарольНачалоВыбора()

&НаКлиенте
Процедура СинхронизироватьНажатие(Элемент)
	
	ОбменМобильноеПриложениеКлиент.Синхронизировать();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Войти(Команда)
	
	Если Не ОбменМобильноеПриложениеВызовСервера.ЕстьНастройкиОбмена() Тогда
		Предупреждение(НСтр("ru='Укажите адрес приложения.';en='Specify the address of the application.'"));
		Возврат;
	КонецЕсли;
	
	Закрыть();
	ОткрытьФорму("ОбщаяФорма.Синхронизация");
	
КонецПроцедуры // Войти()

&НаКлиенте
Процедура Выйти(Команда)
	
	НаборКонстант.СоединениеСЦБУстановлено			= Ложь;
	НаборКонстант.ПарольПользователяЦентральнойБазы	= "";
	
	ЗаписатьКонстанты();
	УстановитьВидимостьИДоступность();
	
	Оповестить("ПрошелОбмен");
	
КонецПроцедуры // Выйти()

#КонецОбласти

#Область ВспомогательныеПроцедурыИФункции

&НаСервере
Процедура ЗаписатьКонстанты()
	
	Набор = РеквизитФормыВЗначение("НаборКонстант");
	Набор.Записать();
	ЗначениеВРеквизитФормы(Набор, "НаборКонстант");
	Модифицированность = Ложь;
	
КонецПроцедуры // ЗаписатьКонстанты()

&НаКлиенте
Процедура УстановитьНаименованиеУстройства()
	
	НаборКонстант.ИмяМобильногоУстройства = "Мобильный клиент (" + НаборКонстант.ПользовательЦентральнойБазы + ")";
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьИДоступность()
	
	ОбменВключен = Константы.СоединениеСЦБУстановлено.Получить();
	
	Элементы.Войти.Видимость					= Не ОбменВключен;
	Элементы.Выйти.Видимость					= ОбменВключен;
	Элементы.ГруппаСинхронизировать.Видимость	= ОбменВключен;
	Элементы.ГруппаИмяПользователя.Доступность	= Не ОбменВключен;
	Элементы.ГруппаПароль.Доступность			= Не ОбменВключен;
	Элементы.ГруппаАдрес.Доступность			= Не ОбменВключен;
	
КонецПроцедуры // УстановитьВидимостьИДоступность()

&НаСервере
Процедура ПроверитьИзменениеНастроекОбмена()
	
	Если СокрЛП(НаборКонстант.АдресЦентральнойБазы) <> СокрЛП(АдресЦентральнойБазыПриОткрытии)
		Или СокрЛП(НаборКонстант.ПользовательЦентральнойБазы) <> СокрЛП(ПользовательЦентральнойБазыПриОткрытии) Тогда
		
		ЦентральныйУзел = ПланыОбмена.бит_мп_МобильноеПриложениеДДС.НайтиПоКоду("001");
		Если Не ЦентральныйУзел.Пустая() Тогда
			ОбменМобильноеПриложениеВызовСервера.ПереинициализироватьСчетчикиСообщенийНаУзлеПланаОбмена(ЦентральныйУзел);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
